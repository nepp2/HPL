
fun alloc(v : T) => ptr(T) with T {
  let p = malloc(sizeof(T)) as ptr(T)
  *p = v
  p
}

struct inner_list(T) {
  len : u64
  data : ptr(T)
  capacity : u64
}

struct list(T) {
  p : ptr(inner_list(T))
}

fun list() => list(T) with T {
  let inner = inner_list.new(0, (0 as u64) as ptr(T), 0)
  list.new(alloc(inner))
}

fun add(list : list(T), item : T) with T {
  let list = list.p;
  let len = list.len
  if list.capacity <= list.len {
    let next_capacity = {
      if list.capacity == 0 { 1 }
      else { list.capacity * 2 }
    }
    let new_data = malloc(next_capacity * sizeof(T))
    memcpy(new_data, list.data as ptr(u8), list.capacity * sizeof(T))
    if (list.data as u64) != 0 {
      free(list.data as ptr(u8))
    }
    list.data = new_data as ptr(T)
    list.capacity = next_capacity
  }
  list.data[list.len] = item
  list.len = list.len + 1
}

fun Index(list : list(T), i : Int) => T with T, Int {
  let list = list.p;
  let i = i as u64
  if i >= list.len {
    panic("list access out of bounds")
  }
  list.data[i]
}

fun print(list : list(T)) with T {
  print("[")
  if list.p.len > 0 {
    print(list[0])
    for x in range(1, list.p.len as i64) {
      print(", ") ; print(list[x])
    }
  }
  print("]")
}

fun println(t : T) with T {
  print(t); println()
}

let nums = list()

for x in range(0, 10) {
  nums.add(x)
}

println(nums)

let strings = list()
strings.add("hello")
strings.add("world")

println(strings)
